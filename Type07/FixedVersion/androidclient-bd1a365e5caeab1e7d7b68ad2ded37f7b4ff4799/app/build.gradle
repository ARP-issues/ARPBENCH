apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'com.github.triplet.play'
apply plugin: 'io.fabric'
apply plugin: 'jacoco-android'

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle){
    source 'src'
    include '**/*.java'
    // code from Google/AOSP
    exclude 'main/java/org/kontalk/util/ThumbnailUtils.java',
        'main/java/org/kontalk/crypto/PRNGFixes.java',
        'main/java/org/kontalk/ui/AccountAuthenticatorActionBarActivity.java',
        'main/java/org/kontalk/ui/prefs/TextMessagePreference.java',
        'main/java/org/kontalk/billing/*.java',
        '*/java/org/kontalk/billing/GoogleBillingService.java',
        '*/java/org/kontalk/billing/Inventory.java',
        '*/java/org/kontalk/billing/ProductDetails.java',
        '*/java/org/kontalk/billing/Purchase.java'
    // 3rd-party code
    exclude '**/CircularSeekBar.java',
        '**/FrameLayoutFixed.java',
        '**/AbsListViewScrollDetector.java',
        '**/XMPPTCPConnection.java',
        '**/ProximityScreenLocker*.java',
        'main/java/com/**', 'main/java/io/**'
    def configProps = ['baseDir': projectDir.absolutePath]
    configProperties configProps
    classpath = files()
}

android {
    compileSdkVersion project.compileSdkVersion
    // temporarly introduced to fix Cyanogenmod bug
    // https://issuetracker.google.com/issues/64434571#comment35
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId project.applicationId
        versionCode project.versionCode
        versionName project.versionName
        targetSdkVersion project.targetSdkVersion
        minSdkVersion project.minSdkVersion
        resConfigs "en", "de", "fr", "it", "es", "ca", "cs", "el", "fa", "gl", "ja", "nl", "pt", "pt-rBR", "ru", "sr", "zh-rCN", "ar", "hi", "tr", "nb-rNO"
        resValue "string", "application_id", applicationId
        resValue "string", "account_type", applicationId + '.account'
        buildConfigField "String", "ACCOUNT_TYPE", '"' + applicationId + '.account"'
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        if (travisBuild) {
            release {
                storeFile rootProject.file('dist/release.keystore')
                storePassword System.getenv("RELEASE_STORE_PASSWORD")
                keyAlias System.getenv("RELEASE_KEY_ALIAS")
                keyPassword System.getenv("RELEASE_KEY_PASSWORD")
            }
        }
        else if (project.hasProperty('RELEASE_STORE_FILE')) {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    flavorDimensions "all"

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            if (project.hasProperty('RELEASE_STORE_FILE') || travisBuild)
                signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            resValue "string", "application_id", defaultConfig.applicationId + '.debug'
            resValue "string", "account_type", defaultConfig.applicationId + '.debug.account'
            buildConfigField "String", "ACCOUNT_TYPE", '"' + defaultConfig.applicationId + '.debug.account"'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg', 'proguard-test.cfg'
            ext.enableCrashlytics = false
            testCoverageEnabled true
        }
    }

    dexOptions {
        // skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    productFlavors {
        basic {
            ext.enableCrashlytics = false
        }
        googleplay {
        }
    }

}

dependencies {
    api (project(':client-common-java')) {
        exclude group: 'net.sf.kxml'
    }

    implementation "com.android.support:appcompat-v7:$appcompatVersion"
    implementation "com.android.support:design:$appcompatVersion"
    implementation "com.android.support:gridlayout-v7:$appcompatVersion"
    implementation "com.android.support:exifinterface:$appcompatVersion"
    implementation "com.android.support:recyclerview-v7:$appcompatVersion"
    implementation "com.android.support:support-dynamic-animation:$appcompatVersion"
    // these are actually to override an emoji dependency
    implementation "com.android.support:cardview-v7:$appcompatVersion"
    implementation "com.android.support:animated-vector-drawable:$appcompatVersion"
    implementation "com.android.support:support-v13:$appcompatVersion"

    implementation "com.madgag.spongycastle:core:$spongyCastleVersion"
    implementation ("com.madgag.spongycastle:prov:$spongyCastleVersion") {
        exclude group: 'junit'
    }
    implementation ("com.madgag.spongycastle:bcpkix-jdk15on:$spongyCastleVersion") {
        exclude group: 'junit'
    }
    implementation ("com.madgag.spongycastle:bcpg-jdk15on:$spongyCastleVersion") {
        exclude group: 'junit'
    }
    implementation 'info.guardianproject.netcipher:netcipher:1.2.1'
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.9.2'
    implementation 'com.google.zxing:core:3.3.1'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation 'com.segment.backo:backo:1.0.0'
    implementation 'com.afollestad:assent:0.2.5'
    implementation "org.igniterealtime.smack:smack-tcp:$smackVersion"
    implementation "org.igniterealtime.smack:smack-experimental:$smackVersion"
    implementation "org.igniterealtime.smack:smack-android:$smackVersion"
    implementation 'com.github.machinarius:preferencefragment:0.1.1'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    implementation 'com.nispok:snackbar:2.11.0'
    // Glide 4 requires API level 14
    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'com.github.instacart.truetime-android:library:3.3'
    implementation 'com.github.daniele-athome:FloatingActionButton:2e5bbbf155'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.github.vlivanov:ListViewVariants:f606578467'
    implementation 'com.vanniktech:emoji-one:0.5.1'

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'

    implementation "com.github.kontalk.AnyMaps:anymaps-base:$anyMapsVersion"
    implementation "com.github.kontalk.AnyMaps:anymaps-osm:$anyMapsVersion"

    googleplayImplementation "com.github.kontalk.AnyMaps:anymaps-google:$anyMapsVersion"
    googleplayImplementation "com.google.android.gms:play-services-maps:$googlePlayServicesVersion"
    googleplayImplementation "com.google.android.gms:play-services-location:$googlePlayServicesVersion"

    googleplayImplementation "com.google.android.gms:play-services-base:$googlePlayServicesVersion"
    googleplayImplementation "com.google.android.gms:play-services-gcm:$googlePlayServicesVersion"
    googleplayImplementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation "com.android.support:support-annotations:$appcompatVersion"
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.1'
    androidTestImplementation 'com.jayway.android.robotium:robotium-solo:5.6.3'
}

if (travisBuild) {
    play {
        track = 'alpha'
        jsonFile = rootProject.file('dist/googleplay-developer-key.json')
        untrackOld = true
    }

    task publish(dependsOn: ['publishGoogleplayRelease']) {
        group = 'Kontalk'
        description = 'Publish to Google Play alpha channel'
    }
    publish.dependsOn 'testGoogleplayReleaseUnitTest'
}
else if (project.hasProperty('GOOGLE_PLAY_DEVELOPER_KEY_FILE')) {
    play {
        track = 'beta'
        jsonFile = file(GOOGLE_PLAY_DEVELOPER_KEY_FILE)
        untrackOld = true
    }
}
